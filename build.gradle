buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.2'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7'
    }
}

apply plugin: 'com.jfrog.bintray'

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'java'
apply plugin: 'maven'

sourceCompatibility = 1.8
targetCompatibility = 1.8
group = 'com.bsycorp'
if (findProperty("appVersion") != null){
    version = findProperty("appVersion")
}

dependencies {
    compile(platform("software.amazon.awssdk:bom:2.13.72"))
    compile("software.amazon.awssdk:ssm")
    compile("software.amazon.awssdk:sts")
    compile("software.amazon.awssdk:dynamodb")
    compile("software.amazon.awssdk:apache-client")
    compile 'org.yaml:snakeyaml:1.20'
    compile "org.slf4j:slf4j-api:1.7.25"
    compile "org.slf4j:slf4j-simple:1.7.25"
    compile "com.beust:jcommander:1.72"
    compile "commons-io:commons-io:2.6"
    compile "com.github.jknack:handlebars:4.0.6"
    // ideally would use the official client but it looks very immature - compile "io.kubernetes:client-java:0.2"
    compile "io.fabric8:kubernetes-client:4.12.0" //this one looks better.
    compile "org.apache.commons:commons-collections4:4.1"
    compile "org.bouncycastle:bcpg-jdk15on:1.60"

    testCompile "com.github.stefanbirkner:system-rules:1.17.0"
    testCompile "junit:junit:4.11"
    testCompile "io.fabric8:kubernetes-server-mock:4.12.0"
    testCompile "org.mockito:mockito-core:1.8.4"
    // We use bouncy-gpg to test the deterministic generated gpg keys
    testCompile("name.neuhalfen.projects.crypto.bouncycastle.openpgp:bouncy-gpg:2.1.0") {
        exclude group: "org.bouncycastle", module: "bcprov-jdk15on"
        exclude group: "org.bouncycastle", module: "bcpg-jdk15on"
    }
    testCompile "org.bouncycastle:bcprov-jdk15on:1.60"
}

jar {
    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
    exclude 'logback.xml'

    manifest {
        attributes 'Main-Class': 'com.bsycorp.kees.ExportMain'
    }
}

shadowJar {
    archiveFileName = "kees-all.jar"
}

artifacts {
    archives jar
    archives shadowJar
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    configurations = ['archives']
    publish = true

    pkg {
        repo = 'public'
        name = 'kees'
        desc = "Kubernetes External Environment Secrets"
        userOrg = 'bsycorp'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/bsycorp/kees.git'

        version {
            name = System.getProperty('appVersion')
            desc = 'Kubernetes External Environment Secrets'
            released = new Date()
            gpg {
                sign = true
            }
        }
    }

}

bintrayUpload.dependsOn(shadowJar)