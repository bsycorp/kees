buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "gradle.plugin.com.palantir.graal:gradle-graal:0.7.1"
        classpath "com.google.gradle:osdetector-gradle-plugin:1.6.2"
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: com.palantir.gradle.graal.GradleGraalPlugin
apply plugin: com.google.gradle.osdetector.OsDetectorPlugin

sourceCompatibility = 1.8
targetCompatibility = 1.8
group = 'com.bsycorp.kees'
if (findProperty("appVersion") != null){
    version = findProperty("appVersion")
}

dependencies {
    compile(project(":base"))

    testCompile "junit:junit:4.11"
    testCompile "org.mockito:mockito-core:1.8.4"
    testCompile "com.github.stefanbirkner:system-rules:1.17.0"
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/bsycorp/kees")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        maven(MavenPublication) {
            groupId = 'com.bsycorp.kees'
            artifactId = 'init'
            version = findProperty("appVersion")
            from components.java
        }
    }
}

def nativeImageOutput = "kees-init-${osdetector.classifier}"
graal {
    mainClass 'com.bsycorp.kees.InitMain'
    outputName nativeImageOutput
    graalVersion "20.2.0"
    javaVersion JavaVersion.VERSION_1_8.majorVersion
    option '-H:IncludeResources=.*keys.json$'
//    option '-H:+PrintMethodHistogram'
//    option '-H:+PrintRuntimeCompileMethods'
//    option '-H:Log=registerResource:'
    option '-H:ReflectionConfigurationFiles=../base/src/main/resources/reflect-config.json'
    option '--enable-http'
    option '--enable-https'
    option '--allow-incomplete-classpath'
    option '--report-unsupported-elements-at-runtime'
    option '--install-exit-handlers'
    option '--no-server'
    option '--no-fallback'
    option '--verbose'
    option '-J-Dfile.encoding=UTF-8'
    option '-J-Xmx2500m' //bound memory so it is of a consistent size may need to increase over time
}